plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def changelogFile = file('changelog.txt')
def dataOutput = 'src/main/generated'

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.kyrptonaught.dev' }
}

dependencies {
    constraints {
        implementation('org.apache.logging.log4j:log4j-core') {
            version {
                strictly('[2.17, 3[')
                prefer('2.17.1')
            }
            because('CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: ' +
                    'Log4j vulnerable to remote code execution and other critical security vulnerabilities')
        }
    }
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${project.geckolib_version}"
    modImplementation 'net.kyrptonaught:customportalapi:0.0.1-beta61-1.19'
    include 'net.kyrptonaught:customportalapi:0.0.1-beta61-1.19'
    include "software.bernie.geckolib:geckolib-fabric-${project.geckolib_version}"
}


loom {
    runs {
        datagen {
            server()

            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file(dataOutput)}"

            runDir "build/datagen"
        }
    }
}

sourceSets { main { resources { srcDirs += dataOutput } } }

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

curseforge {
    //Set environment variable for api key if not set below.
    apiKey = System.getenv('CURSE_API_KEY') ?: ''
    project {
        id = project.curseforge
        changelog = changelogFile
        releaseType = 'release'
        addGameVersion project.minecraft_version
        mainArtifact(remapJar) {
            relations {
                requiredDependency 'fabric-api' // fabric-api is required
            }
        }
        afterEvaluate {
            mainArtifact(remapJar)
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

modrinth {
    //Set environment variable for api key if not set below.
    token = System.getenv('MODRINTH_API_KEY') ?: ''
    projectId = project.modrinth.toString()
    versionType = 'release'
    uploadFile = remapJar
    changelog = changelogFile.getText()
    gameVersions = [project.minecraft_version.toString()]
    loaders = ['fabric']
    dependencies {
        required.project 'fabric-api'
    }
}

//Update the changelog file with correct project version number.
//To change project version number see mod_version in gradle.properties
//NOTE: this will replace the very first line of changelog.txt
tasks.register('addVersionToChangeLog') {
    doLast {
        def firstLine = '[Update ' + version + ']'
        def remainingContent = changelogFile.readLines().drop(1).join(System.lineSeparator())
        changelogFile.write(firstLine + System.lineSeparator() + remainingContent)
    }
}

beforeEvaluate {
    tasks.configureEach { Task task ->
        if (task.name == 'modrinth' || task.name == 'curseforge' + project.curseforge) {
            task.dependsOn addVersionToChangeLog
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}